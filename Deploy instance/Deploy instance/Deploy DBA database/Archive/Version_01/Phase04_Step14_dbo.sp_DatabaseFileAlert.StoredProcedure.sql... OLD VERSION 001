USE [DBA]
GO
/****** Object:  StoredProcedure [dbo].[sp_DatabaseFileAlert]    Script Date: 26/07/2019 12:13:28 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-----------------------------------------------------------------------------------------------------------------------
-- Object Type : Stored Procedure  
-- Object Name : sp_DatabaseFileAlert
-- Description : Stored Procedure to alert if a database has Autogrowth ENABLED or if a database has Autogrowth DISABLED
--				 and its file(s) have exceeded a used capacity alert threshold. The orginaly driver for this stored 
--				 procedure was for the monitoring of databases in an Azure Managed Instance, when Managed Instances had a 
--				 8TB limit... which was an issues for IEMR reporting system, developed by the MSHHS BI team.
-- Author : David Shaw
-- Date : June 2019
-- Version : 0.01
-----------------------------------------------------------------------------------------------------------------------

CREATE PROCEDURE [dbo].[sp_DatabaseFileAlert]
AS
SET NOCOUNT ON
-- ***** START - Declare variables *****
DECLARE @DBID int,
 	    @DB_name varchar (150),
		@DB_name_offline varchar (150),
		@SQL varchar(1000),
		@FileName varchar(500),
        @FileAllocated decimal(11,2),
        @FileUsed decimal(11,2),
		@ID int,
		@AutoGrowth bit,
		--@FILE_DIR AS VARCHAR(250) = 'C:\Windows\Temp\',
		@used_capacity_threshold int = 75, -- this is the threshold for used capacity of a database file as a percentage.
		@environment varchar(50),
		@operator VARCHAR(50),
		@emailsubject VARCHAR(100),
		@emailbody VARCHAR(MAX) = ''
SELECT @environment = [Value] FROM [DBA].[dbo].[Parameters] WHERE [Parameter] = 'Environment'
-- ***** END - Declare variables *****

-- ***** START - Create and populate temporary tables #DataFiles,to hold the datafile stats *****
CREATE TABLE #DataFiles
	(ID int IDENTITY(1,1) NOT NULL,
	 Fileid int,
     [FileGroup] int,
     TotalExtents int,
     UsedExtents int,
	 [Name] varchar(150),
	 [FileName] varchar(1000),
	 DatabaseName varchar(250))
DECLARE cursor_01 CURSOR FOR  
	  SELECT [name] FROM master.sys.databases WHERE [state_desc] = 'ONLINE'	AND [name] NOT IN ('tempdb') AND [name] NOT IN ('ReportServerTempDB')
OPEN cursor_01   
FETCH NEXT FROM cursor_01 INTO @DB_name 
WHILE @@FETCH_STATUS = 0   
	BEGIN
	SET @SQL = 'USE ['+@DB_name+'] INSERT INTO #DataFiles (Fileid, [FileGroup], TotalExtents, UsedExtents, [Name], [FileName]) exec (''DBCC SHOWFILESTATS'')'
	EXEC (@SQL)
	UPDATE #DataFiles SET #DataFiles.DatabaseName = @DB_name WHERE DatabaseName IS NULL
	FETCH NEXT FROM cursor_01 INTO @DB_name   
	END
CLOSE cursor_01   
DEALLOCATE cursor_01
-- ***** END - Create and populate temporary tables #DataFiles,to hold the datafile stats *****

-- ***** START - Create and populate temporary tables #LogFiles, to hold the logfile stats *****
CREATE TABLE #LogFiles
	(ID int IDENTITY(1,1) NOT NULL,
	 [Database ID] int,
	 [Database Name] varchar(150),
	 [Log Size(MB)] decimal(11,2),
	 [Log Space Used(%)] decimal(11,2),
	 [Status] int,
	 [FileName] varchar(500))
INSERT INTO #LogFiles ([Database Name], [Log Size(MB)], [Log Space Used(%)], [Status])exec ('DBCC SQLPERF (LOGSPACE)')
UPDATE #LogFiles SET #LogFiles.[Database ID] = 
	(SELECT [dbid] FROM master.dbo.sysdatabases WHERE [name] = #LogFiles.[Database Name])
-- START - Changes I made to cater for more then one ldf file
UPDATE #LogFiles SET #LogFiles.FileName = 
    (SELECT physical_name FROM sys.master_files WHERE [database_id] = #LogFiles.[Database ID] AND [type] = 1 AND [file_id] <= 2)
UPDATE #LogFiles SET #LogFiles.FileName = 'multiple log files'
		WHERE [Database ID] = (SELECT [database_id] FROM sys.master_files where [database_id] = #LogFiles.[Database ID] AND [type] = 1 AND [file_id] > 2)
-- END - Changes I made to cater for more then one ldf file
-- ***** END - Create and populate temporary tables #LogFiles, to hold the logfile stats *****

-- ***** START - Create temp table #CombinedFiles *****
CREATE TABLE #CombinedFiles
	(ID int IDENTITY(1,1) NOT NULL,
	[DatabaseID] int,
	[DatabaseName] varchar(100),
	[FileName] varchar(500),
	[FileAllocated (MB)] decimal(11, 2),
	[FileUsed (%)] decimal(11, 2),
	[AutoGrowth] bit,
	[RecordedDate] datetime)
-- ***** END - Create temp table #CombinedFiles *****

-- ***** START - Insert data from #DataFiles into #CombinedFiles *****
DECLARE cursor_02 CURSOR FOR  
	SELECT [ID] FROM #DataFiles 
OPEN cursor_02   
FETCH NEXT FROM cursor_02 INTO @ID 
WHILE @@FETCH_STATUS = 0   
	BEGIN
    SELECT @DB_name = [DatabaseName] FROM #DataFiles WHERE [ID] = @ID
	SELECT @DBID = [dbid] FROM master.dbo.sysdatabases WHERE [NAME] = @DB_name
	SELECT @FileName = [FileName] FROM #DataFiles WHERE  [ID] = @ID
	SELECT @FileAllocated = TotalExtents * 64.0 / 1024.0 FROM #DataFiles WHERE  [ID] = @ID
    SELECT @FileUsed = (UsedExtents * 64.0 / 1024.0) / (TotalExtents * 64.0 / 1024.0) * 100 FROM #DataFiles WHERE  [ID] = @ID
	INSERT INTO #CombinedFiles ([DatabaseID],[DatabaseName], [FileName], [FileAllocated (MB)], [FileUsed (%)],[RecordedDate])
		VALUES (@DBID,
				@DB_name,
				@FileName,
				@FileAllocated,
				@FileUsed,				
				getdate())
	FETCH NEXT FROM cursor_02 INTO @ID
	END
CLOSE cursor_02   
DEALLOCATE cursor_02
-- ***** END - Insert data from #DataFiles into #CombinedFiles *****

-- ***** START - Insert data from #LogFiles into #CombinedFiles *****
DECLARE cursor_03 CURSOR FOR  
	SELECT [name] FROM master.dbo.sysdatabases WHERE [version] is not NULL
OPEN cursor_03   
FETCH NEXT FROM cursor_03 INTO @DB_name 
WHILE @@FETCH_STATUS = 0   
	BEGIN
    SELECT @DBID = [dbid] FROM master.dbo.sysdatabases WHERE [NAME] = @DB_name
	SELECT @FileName = [FileName] FROM #LogFiles WHERE [Database Name] = @DB_name
	SELECT @FileAllocated = [Log Size(MB)] FROM #LogFiles WHERE [Database Name] = @DB_name
    SELECT @FileUsed = [Log Space Used(%)] FROM #LogFiles WHERE [Database Name] = @DB_name
	INSERT INTO #CombinedFiles ([DatabaseID],[DatabaseName], [FileName], [FileAllocated (MB)], [FileUsed (%)],[RecordedDate])
		VALUES (@DBID,
				@DB_name,
				@FileName,
				@FileAllocated,
				@FileUsed,				
				getdate())
	FETCH NEXT FROM cursor_03 INTO @DB_name   
	END
CLOSE cursor_03   
DEALLOCATE cursor_03
-- ***** END - Insert data from #LogFiles into #CombinedFiles *****

-- ***** START - Update temp table with Autogrowth setting *****
UPDATE #CombinedFiles  
   SET [AutoGrowth] = (SELECT growth FROM sys.master_files WHERE [physical_name] = #CombinedFiles.[FileName])
-- ***** END - Update temp table with Autogrowth setting *****

-- **************************************************************************************************************************************************************************************
-- **************************************************************************************************************************************************************************************
-- **************************************************************************************************************************************************************************************

-- ***** START - Email notification based on those database(s) with Autogrowth set to true *****
IF EXISTS(SELECT DatabaseName, [FileName], [FileAllocated (MB)], [FileUsed (%)], AutoGrowth FROM #CombinedFiles WHERE [AutoGrowth] = 1)
	BEGIN
	SET @emailbody = 'Below is a list of database(s) that maybe considered troublesum with AutoGrowth ENABLED.' + CHAR(10) + CHAR(10)
	DECLARE @04_DatabaseName varchar(100),
			@04_FileName varchar(500)	
	DECLARE cursor_04 CURSOR FOR  
		SELECT ID FROM #CombinedFiles WHERE [AutoGrowth] = 1  ORDER BY [DatabaseName]
	OPEN cursor_04   
	FETCH NEXT FROM cursor_04 INTO @ID
	WHILE @@FETCH_STATUS = 0   
		BEGIN
		SELECT @04_DatabaseName = [DatabaseName] FROM #CombinedFiles WHERE [ID] = @ID
		SELECT @04_FileName = [FileName] FROM #CombinedFiles WHERE [ID] = @ID				
		SET @emailbody = @emailbody + 'Database "' + @04_DatabaseName + '" file "' + @04_FileName + '" is set true for AutoGrowth.' + CHAR(10)					
		FETCH NEXT FROM cursor_04 INTO @ID   
		END
	CLOSE cursor_04   
	DEALLOCATE cursor_04
	SET @emailsubject = 'MSSQL alert (' + @environment + ') - database(s) violating AutoGrowth setting'	
	
	
	-- **************************************************************************************************************
	-- **************************************************************************************************************
	-- EXEC sys.xp_instance_regread N'HKEY_LOCAL_MACHINE', N'SOFTWARE\Microsoft\MSSQLServer\SQLServerAgent', N'AlertFailSafeOperator', @param = @operator OUT, @no_output = N'no_output'
	SET @operator = 'DBA'
	-- **************************************************************************************************************
	-- **************************************************************************************************************
		
	
	EXEC msdb.dbo.sp_notify_operator @name = @operator, @subject = @emailsubject, @body = @emailbody 
	END
-- ***** END - Email notification based on those database(s) with Autogrowth set to true *****

-- ***** START - Email notification based on those database(s) with no Autogrowth and exceeding the threshold for percentage of file size used *****
IF EXISTS(SELECT DatabaseName, [FileName], [FileAllocated (MB)], [FileUsed (%)], AutoGrowth FROM #CombinedFiles WHERE [AutoGrowth] = 0 AND [FileUsed (%)] > @used_capacity_threshold)
	BEGIN
	SET @emailbody = 'Below is a list of database(s) with AutoGrowth DISABLED that have exceeded the theshold.' + CHAR(10) + CHAR(10)
	DECLARE @05_DatabaseName varchar(100),
			@05_FileName varchar(500),
			@05_FileAllocated decimal(11, 2),
			@05_FileUsed decimal(11, 2)			
	DECLARE cursor_05 CURSOR FOR  
		SELECT ID FROM #CombinedFiles WHERE [AutoGrowth] = 0 AND [FileUsed (%)] > @used_capacity_threshold ORDER BY [DatabaseName]
	OPEN cursor_05   
	FETCH NEXT FROM cursor_05 INTO @ID
	WHILE @@FETCH_STATUS = 0   
		BEGIN
		SELECT @05_DatabaseName = [DatabaseName] FROM #CombinedFiles WHERE [ID] = @ID
		SELECT @05_FileName = [FileName] FROM #CombinedFiles WHERE [ID] = @ID		
		SELECT @05_FileAllocated = [FileAllocated (MB)] FROM #CombinedFiles WHERE [ID] = @ID
		SELECT @05_FileUsed = [FileUsed (%)] FROM #CombinedFiles WHERE [ID] = @ID		
		SET @emailbody = @emailbody + 'Database "' + @05_DatabaseName + '" with AutoGrowth set to false, has consumed ' + CONVERT(varchar(10),@05_FileUsed) + '% of the allocated ' + CONVERT(varchar(10),@05_FileAllocated) + 'MB for the file "' + @05_FileName + '"' + CHAR(10)					
		FETCH NEXT FROM cursor_05 INTO @ID   
		END
	CLOSE cursor_05   
	DEALLOCATE cursor_05
	SET @emailsubject = 'MSSQL alert (' + @environment + ') - database(s) exceeding used capacity threshold (' + CONVERT(varchar(5),@used_capacity_threshold) + '%)'
	
	
	-- **************************************************************************************************************
	-- **************************************************************************************************************
	--   EXEC sys.xp_instance_regread N'HKEY_LOCAL_MACHINE', N'SOFTWARE\Microsoft\MSSQLServer\SQLServerAgent', N'AlertFailSafeOperator', @param = @operator OUT, @no_output = N'no_output'	
	SET @operator = 'DBA'
	-- **************************************************************************************************************
	-- **************************************************************************************************************
	
	
	EXEC msdb.dbo.sp_notify_operator @name = @operator, @subject = @emailsubject, @body = @emailbody
	END
-- ***** END - Email notification based on those database(s) with no Autogrowth and exceeding the threshold for percentage of file size used *****
	
-- ***** START - Drop up temporary tables *****
DROP TABLE #DataFiles
DROP TABLE #LogFiles
DROP TABLE #CombinedFiles
-- ***** END - Drop up temporary tables *****
