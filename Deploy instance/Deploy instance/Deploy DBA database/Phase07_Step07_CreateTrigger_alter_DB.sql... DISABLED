USE [Master]
GO
-- ***** Create trigger to monitor the alteration of databases *****
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

IF EXISTS (SELECT * FROM sys.server_triggers WHERE name = 'trig_alter_database')
DROP TRIGGER trig_alter_database
ON ALL SERVER
GO

CREATE TRIGGER trig_alter_database 
ON ALL SERVER 
FOR ALTER_DATABASE 
AS 
	BEGIN
	DECLARE @UserName VARCHAR(200),
            @Servername VARCHAR(50), 
            @emailsubject VARCHAR(200),
			@TSQL_statement VARCHAR(500),
			@emailbody VARCHAR(1000),
			@operator VARCHAR(50)
	SELECT @UserName = original_login()
	SELECT @Servername = @@servername 
	SELECT @TSQL_statement = EVENTDATA().value('(/EVENT_INSTANCE/TSQLCommand/CommandText)[1]','nvarchar(max)')
	SET @emailsubject = 'MSSQL alter database notification for ' + @servername
	SET @emailbody = @UserName + ', has just altered the following database on ' + @servername + ' with the following TSQL...' + CHAR(13) + CHAR(10) + @TSQL_statement

	-- ***** Email out alerts *****
	EXEC sys.xp_instance_regread N'HKEY_LOCAL_MACHINE', N'SOFTWARE\Microsoft\MSSQLServer\SQLServerAgent', N'AlertFailSafeOperator', @param = @operator OUT, @no_output = N'no_output'
	EXEC msdb.dbo.sp_notify_operator @name = @operator,
									 @subject = @emailsubject,
									 @body = @emailbody
	END
GO



--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************
--  ******************* Would need to be mindful that someone with db_owner to a user database, would not have permission to fire this trigger *******************